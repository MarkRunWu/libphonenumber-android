buildscript {
  group = 'io.michaelrocks'
  version = '8.13.4'

  ext.projectCompileSdkVersion = 33
  ext.projectBuildToolsVersion = '33.0.0'
  ext.projectMinSdkVersion = 14
  ext.projectTargetSdkVersion = 33

  ext.androidToolsVersion = '7.3.1'

  ext.junitVersion = '4.13.2'
  ext.mockitoVersion = '4.8.0'

  ext.androidxEspressoVersion = '3.4.0'
  ext.androidxTestRulesVersion = '1.4.0'
  ext.androidxTestRunnerVersion = '1.4.0'
  ext.androidxTestExtJunitVersion = '1.1.3'

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:$androidToolsVersion"
  }
}

allprojects {
  repositories {
    google()
    mavenCentral()
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

allprojects { project ->
  def publishPropertiesFile = rootProject.file('publish.properties')
  if (publishPropertiesFile.exists()) {
    def localProperties = new Properties()
    publishPropertiesFile.withInputStream { stream -> localProperties.load(stream) }
    localProperties.each { property ->
      if (property.key == "signing.secretKeyRingFile") {
        // If this is the key ring, treat it as a relative path
        project.ext.set(property.key, rootProject.file(property.value).absolutePath)
      } else {
        project.ext.set(property.key, property.value)
      }
    }
  }
}
